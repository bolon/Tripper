apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    def versionPropsFile = new File('version.properties')

    if (versionPropsFile.canRead()) {
        def vcode = getVersionCode()
        def vname = getVersionName()

        incrementVersionProp(versionPropsFile)

        defaultConfig {
            applicationId "com.nnd.tripper"
            minSdkVersion 21
            targetSdkVersion 26
            versionCode vcode
            versionName vname
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
    } else {
        throw new GradleException("Could not read version.properties")
    }

    buildTypes.each {
        //it.buildConfigField "String", "[some_api_key]", getProperties("[api_key_name]")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

/**
 * Get properties from private file
 * @param property property name
 * @return value of property
 */
def getProperties(String property) {
    Properties properties = new Properties()
    properties.load(new FileInputStream("$rootDir/key.properties"))

    return "\"" + properties.getProperty(property) + "\""
}

def getVersionCode() {
    Properties properties = new Properties()
    properties.load(new FileInputStream("$rootDir/version.properties"))

    def major = properties['major'] as int // 1..∞
    def minor = properties['minor'] as int // 1..99
    def build = properties['build'] as int // 0..999

    return (major * 100 + minor) * 1000 + build;
}

def getVersionName() {
    Properties properties = new Properties()
    properties.load(new FileInputStream("$rootDir/version.properties"))

    def versionName = properties['major'] + "." + properties['minor'] + "." + properties['build']

    return versionName;
}

def incrementVersionProp(File versionFile) {
    Properties properties = new Properties()
    properties.load(new FileInputStream("$rootDir/version.properties"))

    def major = properties['major'] as int // 1..∞
    def minor = properties['minor'] as int // 1..99
    def build = properties['build'] as int // 0..999

    if (build + 1 > 999) {
        if (minor + 1 > 99)
            major = major + 1;
        else
            minor = minor + 1;
    } else
        build = build + 1;

    properties['build'] = build.toString()
    properties['minor'] = minor.toString()
    properties['major'] = major.toString()
    properties.store(versionFile.newWriter(), null)
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:design:$androidAppCompatVersion"
    compile "com.android.support:appcompat-v7:$androidAppCompatVersion"
    compile "com.android.support:cardview-v7:$androidAppCompatVersion"

    compile "org.jetbrains.anko:anko:$ankoVersion"
    compile "org.jetbrains.anko:anko-commons:$ankoVersion"

    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    compile "org.parceler:parceler-api:$parcelerVersion"
    kapt "org.parceler:parceler:$parcelerVersion"

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    compile "com.airbnb.android:lottie:$lottieVersion"

    compile "com.android.support.constraint:constraint-layout:$androidConstraintLayoutVersion"

    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$rxRetrofitAdptVersion"

    compile "com.jakewharton.timber:timber:$timberVersion"

    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
}
repositories {
    mavenCentral()
}
